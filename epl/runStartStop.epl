#include "fedMasks.epl"
#include "streamARate.epl"
#include "level1TriggerRate.epl"
#include "triggerLasDeadtime.epl"

create objectarray schema RunStart as (daqState String);
create objectarray schema RunStop as (daqState String);

create variable boolean isRunOn = false;
/** initialize when the expert starts and knows not the current state**/
on pattern[p=levelZeroFM_subsys(SUBSYS='DAQ')] set isRunOn=p.STATE.equals('Running');
on pattern[every RunStart] set isRunOn=true;
on pattern[every RunStop] set isRunOn=false;

@Watched(label='Running')
select isRunOn from pattern[every timer:interval(333 msec)];

insert into RunStop select p.toState as daqState from 
	pattern[every p=DaqStateChangeStream(toState in (
		'Configured', 'Halted', 'Resetting', 'Paused'
	))] where isRunOn=true;

insert into RunStart select p.toState as daqState from
	pattern[every p=DaqStateChangeStream(
		toState='Running'
	)] where isRunOn=false;


/** some information to present upon the end of the run **/
create context CurrentRun initiated by pattern[every(RunStart or levelZeroFM_subsys(SUBSYS='DAQ', STATE='Running'))] terminated by pattern [every RunStop];


create objectarray schema RunAvgRate as (rate double);
create objectarray schema RunAvgARate as (rate double);
create objectarray schema RunDuration as (mili long);
create objectarray schema RunAvgTrgLasDt as (dt double);

create expression runAvgRate {(select rate from RunAvgRate.win:length(1))};
create expression runAvgARate {(select rate from RunAvgARate.win:length(1))};
create expression runDuration {(select mili from RunDuration.win:length(1))};
create expression subsystems {(select concat(SUBSYS) from levelZeroFM_subsys.std:unique(SUBSYS) where INOUT='In')};
create expression runNumber {(select RUN_NUMBER from levelZeroFM_static.win:length(10).std:unique(SID) where SID=sid)};
create expression avgTrgLasDt {(select dt from RunAvgTrgLasDt.win:length(1))};

context CurrentRun insert into RunAvgARate select avg(rate) as rate from StreamRates where name='A';
context CurrentRun insert into RunAvgRate select avg(rate) as rate from L1Rates;
context CurrentRun insert into RunDuration select max(current_timestamp())-min(current_timestamp()) as mili from pattern [every timer:interval(100msec)];
context CurrentRun insert into RunAvgTrgLasDt select avg(dt) as dt from TriggerLasDtStream;

/** Message on Run Start giving: Run NR, SID, Detectors in, (*)Feds in per detectoroverview **/
@Verbose(label='output-start-stop', extraNfo="RunStart", fields={'systime','sid','runNumber','detectors','fedsPerSubsystem'})
select sid, date(current_timestamp()) as systime, runNumber() as runNumber, fedsForSubsysMap() as fedsPerSubsystem, subsystems() as detectors from pattern [every RunStart] unidirectional;

/** Message on Run Stop giving: Run Nr, SID, Detectors in, (*)Feds in per detector, avg L1 rate, avg stream A rate, avg dead time (from trigger LAS), duration **/
@Verbose(label='output-start-stop', extraNfo="Run Stop", fields={'systime','sid','runNumber','runDuration','avgRunRate','avgStreamARate', 'avgTrgLasDt', 'detectors', 'fedsPerSubsystem'})
select date(current_timestamp()) as systime, sid, subsystems() as detectors, runNumber() as runNumber, fedsForSubsysMap() as fedsPerSubsystem, 
	runAvgRate() as avgRunRate, runAvgARate() as avgStreamARate, formatMs(runDuration()) as runDuration, avgTrgLasDt() as avgTrgLasDt
	from pattern [every RunStop] unidirectional;



