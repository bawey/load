#include "zeroRateContext.epl"
#include "fedFractions.epl"

context L1ZeroRate create window FedResyncInfos.std:unique(fedSrcId) as (fedSrcId int, myrinetLastResyncEvt long, myrinetResync long, systime long);

@Description('When rate has been zero for long enough, count the clusters of myrinetLastResyncEvt values')
context L1ZeroRate 
	insert into 
		FedResyncInfos 
	select 
		lnk.fedSrcId as fedSrcId, 
		crd.myrinetLastResyncEvt as myrinetLastResyncEvt, 
		crd.myrinetResync as myrinetResync,
		current_timestamp() as systime
	from 	
		frlcontrollerCard.std:unique(context, slotNumber) as crd, 
		FrlCtlLnk.std:unique(fedSrcId) as lnk
	where	
		lnk.context = crd.context and lnk.slotNumber = crd.slotNumber;


create objectarray schema FedResyncSummary as (systime long, lastResyncUniq long, resyncUniq long, fedsNo long);

context L1ZeroRate insert into FedResyncSummary 
	select
		current_timestamp() as systime,
		count(distinct myrinetLastResyncEvt) as lastResyncUniq,
		count(distinct myrinetResync) as resyncUniq,
		count(fedSrcId) as fedsNo
	from 
		pattern[FedResyncInfos->(timer:interval(1 msec) and not FedResyncInfos)] unidirectional, FedResyncInfos
	output
		first every 10 seconds;

@Priority(10)
create context MyrinetLastResyncMisaligned
	start FedResyncSummary(lastResyncUniq>1)
	end FedResyncSummary(lastResyncUniq=1);

@Priority(10)
create context MyrinetResyncMisaligned
	start FedResyncSummary(resyncUniq>1)
	end FedResyncSummary(resyncUniq=1);

@Verbose(label="output2", fields={'systime','fedsNo','myrinetLastResyncEvt','feds'}, extraNfo="myrinetLastResyncEvt discrepancy" )
	context MyrinetLastResyncMisaligned
	select 
		date(x.systime) as systime, 
		count(fedSrcId) as fedsNo, 
		myrinetLastResyncEvt,
		fedsInfoString(window(x.*).toMap(k=>k.fedSrcId, v=>null)) as feds 
	from 
		pattern [every FedResyncSummary] unidirectional,
		FedResyncInfos as x 
	group by 
		myrinetLastResyncEvt
	order by 
		count(fedSrcId) desc;


@Verbose(label="output2", fields={'systime','fedsNo','myrinetResync','feds'}, extraNfo="myrinetResync discrepancy")
	context MyrinetResyncMisaligned
	select 
		date(x.systime) as systime, 
		count(fedSrcId) as fedsNo, 
		myrinetResync, 	
		fedsInfoString(window(x.*).toMap(k=>k.fedSrcId, v=>null)) as feds 
	from 
		pattern[every FedResyncSummary] unidirectional, 
		FedResyncInfos as x 
	group by 
		myrinetResync
	order by
		count(fedSrcId) desc;


/** FEDS STUCK WIT BACKPRESSURE WITH ZERO RATE **/
@Verbose(label='output2', fields={'systime','feds','srctime'}, extraNfo='FEDs stuck with Backpressure at zero rate')
context L1ZeroRate
	select 
		date(max(b.timestamp)) as srctime, 
		date(current_timestamp()) as systime,
		fedsInfoString(window(b.*).toMap(k=>k.fedSrcId, v=>format(v.bpFraction))) as feds 
	from 
		FrlBackpressure(timestamp is not null) as b,
		pattern[every FrlBackpressure->(timer:interval(1 msec) and not FrlBackpressure)] unidirectional
	output
		first every 5 seconds;


@Verbose(label='output2', fields={'systime','feds', 'srctime'}, extraNfo='FEDs stuck with Deadtime at zero rate')
context L1ZeroRate
	select 
		date(max(b.timestamp)) as srctime, 
		date(current_timestamp()) as systime, 
		fedsInfoString(window(b.*).toMap(k=>k.fedSrcId, v=>format(v.dtFraction))) as feds 
	from 
		FmmDeadtime as b, 
		pattern[every FmmDeadtime->(timer:interval(1 msec) and not FmmDeadtime)] unidirectional
	output
		first every 5 seconds;


@Verbose(label="output3", fields={'systime','feds', 'srctime'}, extraNfo='FEDs stuck')
context L1ZeroRate
	select		
		fedsInfoString(window(ff.*).toMap(
				k=>k.fedSrcId,
				v=>
					"« "||
						(case when v.dOos>0 then "dOos: "||(100*v.dOos/v.dTime).toString()||"% " else "" end) ||
						(case when v.dBusy>0 then "dBusy: "||(100*v.dBusy/v.dTime).toString()||"% " else "" end) ||
						(case when v.dWarning>0 then "dWarning: "||(100*v.dWarning/v.dTime).toString()||"% " else "" end) ||
						(case when v.dError>0 then "dError: "||(100*v.dError/v.dTime).toString()||"% " else "" end) ||
						(case when v.dTime>(v.dBusy+v.dWarning+v.dError+v.dOos+v.dReady) then 
						", dIllegal "|| (100*(v.dTime-(v.dBusy+v.dWarning+v.dError+v.dOos+v.dReady))/v.dTime).toString()||"% " else "" end)||
					"»"
			)) as feds,

		max(timestamp) as srctime, 
		date(current_timestamp()) as systime
	from 
		FedIndicators.win:time(2 msec) as ff,
		pattern[every FedIndicators->(timer:interval(1 msec) and not FedIndicators)] unidirectional
	where 
		dTime>0 and (dBusy=dTime or dWarning=dTime or dError = dTime or dOos = dTime or (dBusy+dWarning+dError+dOos+dReady)=0);
